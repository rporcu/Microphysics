!******************************************************************
!
!  DES input generator
!  Sample generator to generate particle positions and outputs in DES readable format
!  Please use this as a basis and perform any additional customizations as
!  needed and this module does not come with any guarantees
!
!  The particles generated by this code are greater than or equal to specified np and
!  MFIX will pick up the correct particles according to value of np specified in the code
!
!  TO DO:
!  1) Fix so that only np particle information is created
!  2) In MFIX read the entire input file and if the number of particles does not correspond to
!     the input deck, than flag an error. 
!
!  Author: Jay Boyalakuntla  (May-12-06)
! Modified: S. Pannala (Nov-21-06)
! Modified: wdf (2-25-2015)
!
!******************************************************************
!
!
      PROGRAM DES_Particle_Genrator 
!
!
        IMPLICIT NONE
!
        DOUBLE PRECISION, PARAMETER :: PI = 4.0d0*ATAN(1.0d0)
!
        INTEGER :: i,  j,  k
        INTEGER :: ii, jj, kk
        INTEGER :: phase, pgtype 
        INTEGER :: np
        INTEGER :: Ni, Nj, Nk
!
        DOUBLE PRECISION :: xl, yl, zl
        DOUBLE PRECISION :: x0, y0, z0
        DOUBLE PRECISION :: diameter, density 
        DOUBLE PRECISION :: Us, Vs, Ws, T_0
        DOUBLE PRECISION :: radius, dia, rad
        DOUBLE PRECISION :: xp, yp, zp, dist
        DOUBLE PRECISION :: dx, dy, dz
        DOUBLE PRECISION :: mean_u, mean_v, mean_w, temp
        DOUBLE PRECISION :: ru(4)
!
        DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE :: x, y, z
        DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE :: u, v, w
!
        OPEN(unit=10, file="Pgen.in", status='old')
        OPEN(unit=20, file="particle_input.dat", status='replace')
!
        READ (10,*) phase     ! 0 = classic, else exa phase index
        READ (10,*) pgtype    ! 1-5 = rand box, rand cyc, rand cyl, sq box, sq cyl
        READ (10,*) diameter  ! particle diameter
        READ (10,*) density   ! particle density
        READ (10,*) np        ! number of particles
        READ (10,*) xl        ! domain x-dir size
        READ (10,*) yl        ! domain y-dir size
        READ (10,*) zl        ! domain z-dir size
        READ (10,*) x0        ! shift in x-dir
        READ (10,*) y0        ! shift in y-dir
        READ (10,*) z0        ! shift in z-dir
        READ (10,*) T_0       ! thermal speed 
        READ (10,*) Us        ! mean vel in x-dir
        READ (10,*) Vs        ! mean vel in y-dir
        READ (10,*) Ws        ! mean vel in z-dir
!
        ALLOCATE(x(np),y(np),z(np))
        ALLOCATE(u(np),v(np),w(np))
!
        radius = 0.5d0*diameter
        rad = (1.0d0 + 1.0d-4)*radius  ! so that particles don't touch each other to begin with
        dia = 2.0d0*rad

        CALL RANDOM_SEED

!       seed the particles
        WRITE(*,*) '  '
        SELECT CASE (pgtype)
          CASE (1)           
!         random box with rad buffer
          WRITE(*,*) '   random lattice in a box'
            DO i = 1, np
 11           CONTINUE
              CALL RANDOM_NUMBER(ru(1:3))
              x(i) = DBLE(ru(1))*(xl-dia) + rad
              y(i) = DBLE(ru(2))*(yl-dia) + rad
              z(i) = DBLE(ru(3))*(zl-dia) + rad
              DO j = 1, i-1
                dist = DSQRT((x(i)-x(j))**2 + (y(i)-y(j))**2 +&
                             (z(i)-z(j))**2)
                IF (dist .LE. dia) GOTO 11 !failed, try again
              END DO
            END DO
!
          CASE (2)           
!         random box with periodic aware edges
          WRITE(*,*) '   cyclic random lattice'
            DO i = 1, np
 12           CONTINUE
              CALL RANDOM_NUMBER(ru(1:3))
              x(i) = DBLE(ru(1))*xl  
              y(i) = DBLE(ru(2))*yl
              z(i) = DBLE(ru(3))*zl
              DO j = 1, i-1
                DO kk = -1, 1
                DO jj = -1, 1
                DO ii = -1, 1
                  xp = x(j) + DBLE(ii)*xl
                  yp = y(j) + DBLE(jj)*yl
                  zp = z(j) + DBLE(kk)*zl
                  dist = DSQRT((x(i)-xp)**2 + (y(i)-yp)**2 +&
                               (z(i)-zp)**2) 
                  IF (dist .LE. dia) GOTO 12 !failed, try again
                END DO
                END DO
                END DO
              END DO
            END DO
!
          CASE (3)
!         random cyl with rad buffer
          WRITE(*,*) '   random lattice in a pipe'
            DO i = 1, np
 13           CONTINUE
              CALL RANDOM_NUMBER(ru(1:3))
              x(i) = DBLE(ru(1))*(xl-dia) + rad
              y(i) = DBLE(ru(2))*(yl-dia) + rad
              z(i) = DBLE(ru(3))*(zl-dia) + rad
              dist = (x(i)/(xl/2.0d0-dia))**2 &  !+1rad buffer
                   + (z(i)/(zl/2.0d0-dia))**2 
              IF (dist .GE. 1.0d0) GOTO 13  !failed, outside cyl
              DO j = 1, i-1
                dist = DSQRT((x(i)-x(j))**2 + (y(i)-y(j))**2 +&
                             (z(i)-z(j))**2)
                IF (dist .LE. dia) GOTO 13  !failed, try again
              END DO
            END DO
!
          CASE (4) 
!         square packing in box
          WRITE(*,*) '   ordered cubic array in a box'
            Ni = FLOOR(xl/dia)
            Nj = FLOOR(yl/dia)
            Nk = FLOOR(zl/dia)
            dx = xl/DBLE(Ni)
            dy = yl/DBLE(Nj)
            dz = zl/DBLE(Nk)
!
            i = 1
            DO jj = 1, Nj
              DO kk = 1, Nk
                DO ii = 1, Ni
                  IF (i .GT. np) CYCLE     !failed, we already generated all needed particles
                  x(i) = (DBLE(ii) - 0.5d0)*dx
                  y(i) = (DBLE(jj) - 0.5d0)*dy
                  z(i) = (DBLE(kk) - 0.5d0)*dx
                  i    = i + 1
                END DO
              END DO
            END DO
!
          CASE (5) 
!         square packing in pipe
          WRITE(*,*) '   ordered cubic array in a pipe'
            Ni = FLOOR(xl/dia)
            Nj = FLOOR(yl/dia)
            Nk = FLOOR(zl/dia)
            dx = xl/DBLE(Ni)
            dy = yl/DBLE(Nj)
            dz = zl/DBLE(Nk)
!
            i = 1
            DO jj = 1, Nj
              DO kk = 1, Nk
                DO ii = 1, Ni
                  IF (i .GT. np) CYCLE     !failed, we already generated all needed particles
                  x(i) = (DBLE(ii) - 0.5d0)*dx
                  y(i) = (DBLE(jj) - 0.5d0)*dy
                  z(i) = (DBLE(kk) - 0.5d0)*dx
                  dist = (x(i)/(xl/2.0d0-dia))**2 &  !+1rad buffer
                       + (z(i)/(zl/2.0d0-dia))**2
                  if (dist .LT. 1.0d0) i = i + 1
                END DO
              END DO
            END DO
!
          CASE DEFAULT 
!         invalid particl gen type
          WRITE(*,*) 'invalid particle gen type selected'
          x(:) = 0.0d0
          y(:) = 0.0d0
          z(:) = 0.0d0       
        END SELECT 
        WRITE(*,*) '  '

!       shift the particles in space
        x(:) = x(:) + x0
        y(:) = y(:) + y0
        z(:) = z(:) + z0

!
!       generate random particle velocities 
        DO i = 1, np
          CALL RANDOM_NUMBER(ru)
!         random velocities uniform -> standard normal via Box-Muller 
          u(i) = DSQRT(-2.0d0*DLOG(DBLE(ru(1))))*COS(2.0d0*PI*ru(2))
          v(i) = DSQRT(-2.0d0*DLOG(DBLE(ru(1))))*SIN(2.0d0*PI*ru(2))
          w(i) = DSQRT(-2.0d0*DLOG(DBLE(ru(3))))*COS(2.0d0*PI*ru(4))
        END DO

!       calc the average mean velocity in each direction 
        mean_u = 0.0d0
        mean_v = 0.0d0
        mean_w = 0.0d0
        DO i = 1, np
          mean_u = mean_u + u(i)
          mean_v = mean_v + v(i)
          mean_w = mean_w + w(i)
        END DO
        mean_u = mean_u / DBLE(np)
        mean_v = mean_v / DBLE(np)
        mean_w = mean_w / DBLE(np)
 
!       Subtract mean velocities from each particle random velocities
!       so new means are zero
!       Also, calc mean granular temperature while you're at it 
        temp = 0.0d0
        DO i = 1, np
          u(i) = u(i) - mean_u
          v(i) = v(i) - mean_v
          w(i) = w(i) - mean_w
          temp = temp + (u(i)**2 + v(i)**2 + w(i)**2)/3.0d0
        END DO
        temp = temp/DBLE(np)

!       Adjust velocities so that the mean granular temperature is equal
!       to the desired initial granular temperature from the input
        u = u*DSQRT(T_0/temp) 
        v = v*DSQRT(T_0/temp) 
        w = w*DSQRT(T_0/temp) 

!       Add the mean solids flows
        u(:) = u(:) + Us
        v(:) = v(:) + Vs
        w(:) = w(:) + Ws


!       Print to particle_input.dat
 812    FORMAT (3(e22.14,2x),2(e12.4,2x),3(e22.14,2x))
 813    FORMAT (1(i4,2x),3(e22.14,2x),2(e12.4,2x),3(e22.14,2x))
        IF (phase .NE. 0) WRITE(20,*) np
        DO i = 1, np
          IF (phase .EQ. 0) THEN
            WRITE(20,812) x(i), y(i), z(i), radius, density, u(i), v(i), w(i)
          ELSE
            WRITE(20,813) phase, y(i), x(i), z(i), radius, density, v(i), u(i), w(i)
          ENDIF
        END DO

        STOP
      END PROGRAM 
!
