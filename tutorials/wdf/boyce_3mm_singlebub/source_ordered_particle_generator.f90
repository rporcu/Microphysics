!******************************************************************
!
!  DES input generator
!  Sample generator to generate particle positions and outputs in DES readable format
!  Please use this as a basis and perform any additional customizations as
!  needed and this module does not come with any guarantees
!
!  The particles generated by this code are greater than or equal to specified np and
!  MFIX will pick up the correct particles according to value of np specified in the code
!
!  TO DO:
!  1) Fix so that only np particle information is created
!  2) In MFIX read the entire input file and if the number of particles does not correspond to
!     the input deck, than flag an error. 
!
!  Author: Jay Boyalakuntla  (May-12-06)
! Modified: S. Pannala (Nov-21-06)
! Modified: wdf (2-25-2015)
!
!******************************************************************
!
!
      PROGRAM DES_Particle_Genrator 
!
!
        IMPLICIT NONE
!
        DOUBLE PRECISION, PARAMETER :: PI = 4.0d0*ATAN(1.0d0)
!
        INTEGER :: code
        INTEGER :: accept, touch, ia
        INTEGER :: i, j, k
        INTEGER :: n, np, np1
        INTEGER :: ii, jj, kk 
        INTEGER :: Ni, Nj, Nseed
!
        DOUBLE PRECISION :: xp, yp, zp
        DOUBLE PRECISION :: rl, yl, x0l, z0l
        DOUBLE PRECISION :: diameter, radius, density, T_0
        DOUBLE PRECISION :: dia, rad, dist, mean_u, mean_v, mean_w, temp
        DOUBLE PRECISION :: dx, dy, dz
        DOUBLE PRECISION, DIMENSION(4) :: ru 
!
        DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE :: x, y, z
        DOUBLE PRECISION, DIMENSION(:), ALLOCATABLE :: u, v, w
!
!(hardcoded below)        OPEN(unit=10, file="Pgen.in", status='old')
        OPEN(unit=20, file="particle_input.dat", status='replace')
!
!       Pgen.in 
!
        code     = 1           ! 0 = classic, else exa
        diameter = 2.93d-3     ! dp
        density  = 1040.0      ! rho_s
        T_0      = 1.0d-6      ! T_0
        np       = 260655      ! Np (serial)
        rl       = 0.095d0     ! R
        yl       = 0.3d0       ! Ly
        x0l      = 0.096d0     ! x0
        z0l      = 0.096d0     ! z0
!
        ALLOCATE(x(np),y(np),z(np))
        ALLOCATE(u(np),v(np),w(np))
!
        radius = 0.5d0*diameter
        rad = (1.0d0 + 1.0d-3)*radius  ! so that particles don't touch each other to begin with
        dia = 2.0d0*rad
        ru(:) = 0.0d0

        CALL RANDOM_SEED

!       Calc vec sizes and dels
        Ni = FLOOR(rl/rad)        
        Nj = FLOOR(yl/dia)        
        dx = 2.0d0*rl/DBLE(Ni)
        dy = yl/DBLE(Nj)

        Nseed = 1
        DO jj = 1, Nj
          DO kk = 1, Ni
            DO ii = 1, Ni
              IF (Nseed .GT. np) CYCLE     !failed, we already generated all needed particles
              xp = (DBLE(ii) - 0.5d0)*dx - rl
              yp = (DBLE(jj) - 0.5d0)*dy + rad !+1rad buffer
              zp = (DBLE(kk) - 0.5d0)*dx - rl
              dist = DSQRT((xp**2 + zp**2))/(rl - dia)  !+1rad buffer
              IF (dist .GE. 1.0d0) CYCLE   !failed, outside cyl radius
              x(Nseed) = xp
              y(Nseed) = yp
              z(Nseed) = zp
              Nseed = Nseed + 1
            END DO
          END DO
        END DO

!       generate random particle velocities 
        DO i = 1, np
          CALL RANDOM_NUMBER(ru)
!         random velocities uniform -> standard normal via Box-Muller 
          u(i) = DSQRT(-2.0d0*DLOG(DBLE(ru(1))))*COS(2.0d0*PI*ru(2))
          v(i) = DSQRT(-2.0d0*DLOG(DBLE(ru(1))))*SIN(2.0d0*PI*ru(2))
          w(i) = DSQRT(-2.0d0*DLOG(DBLE(ru(3))))*COS(2.0d0*PI*ru(4))
        END DO

!       calc the average mean velocity in each direction 
        mean_u = 0.0d0
        mean_v = 0.0d0
        mean_w = 0.0d0
        DO i = 1, np
          mean_u = mean_u + u(i)
          mean_v = mean_v + v(i)
          mean_w = mean_w + w(i)
        END DO
        mean_u = mean_u / DBLE(np)
        mean_v = mean_v / DBLE(np)
        mean_w = mean_w / DBLE(np)
 
!       Subtract mean velocities from each particle random velocities
!       so new means are zero
!       Also, calc mean granular temperature while you're at it 
        temp = 0.0d0
        DO i = 1, np
          u(i) = u(i) - mean_u
          v(i) = v(i) - mean_v
          w(i) = w(i) - mean_w
          temp = temp + (u(i)**2 + v(i)**2 + w(i)**2)/3.0d0
        END DO
        temp = temp/DBLE(np)

!       Adjust velocities so that the mean granular temperature is equal
!       to the desired initial granular temperature from the input
        u = u*DSQRT(T_0/temp) 
        v = v*DSQRT(T_0/temp) 
        w = w*DSQRT(T_0/temp) 

!       Print to particle_input.dat
        IF (code .NE. 0) WRITE(20,*) np
 12     FORMAT (3(e22.14,2x),2(e12.4,2x),3(e22.14,2x))
 13     FORMAT (1(i4,2x),3(e22.14,2x),2(e12.4,2x),3(e22.14,2x))
        DO i = 1, np
          IF (code .EQ. 0) THEN
            WRITE(20,12) x(i) + x0l, y(i), z(i) + z0l, radius, density, u(i), v(i), w(i)
          ELSE
            WRITE(20,13) 1, y(i), x(i) + x0l, z(i) + z0l, radius, density, u(i), v(i), w(i)
          ENDIF
        END DO

        CLOSE(20)

        STOP
      END PROGRAM 
!
!
